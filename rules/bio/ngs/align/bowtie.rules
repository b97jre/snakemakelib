# -*- snakemake -*-
import os
from snakemakelib.config import update_sml_config, get_sml_config
from snakemakelib.bio.ngs.db import index
from snakemakelib.bio.ngs.rnaseq.tuxedo import TuxedoReadGroup

include: '../settings.rules'
include: '../tools/samtools.rules'

ngs_cfg = get_sml_config('bio.ngs.settings')

config_default = {
    'bio.ngs.align.bowtie' : {
        'index' : index,
        'ref' : ngs_cfg['db']['ref'],
        'rg_fn' : TuxedoReadGroup(regexp=ngs_cfg['sampleorg'].run_id_re.re.pattern).parse,
        'version2' : True,
        'bowtie' : {
            'threads' : ngs_cfg['threads'],
            'options' : '',
        },
        'build' : {
            'ext_v1' : [".1.ebwt", ".2.ebwt", ".3.ebwt", ".4.ebwt", ".rev.1.ebwt", ".rev.2.ebwt"],
            'ext_v2' : [".1.bt2", ".2.bt2", ".3.bt2", ".4.bt2", ".rev.1.bt2", ".rev.2.bt2"],
        },
    },
}

update_sml_config(config_default)

bwt_cfg = get_sml_config('bio.ngs.align.bowtie')
samtools_cfg = get_sml_config('bio.ngs.tools.samtools')

rule bowtie_align:
    """Bowtie paired end alignment"""
    params: cmd = 'bowtie2' if bwt_cfg['version2'] else 'bowtie',
            options = bwt_cfg['bowtie']['options'],
            index = ("-x " if bwt_cfg['version2'] else "") + bwt_cfg['index', 'bowtie']
    input: read1="{prefix}" + ngs_cfg['read1_label'] + ngs_cfg['fastq_suffix'],\
           read2="{prefix}" + ngs_cfg['read2_label'] + ngs_cfg['fastq_suffix']
    output: "{prefix}.bam"
    threads: bwt_cfg['bowtie']['threads']
    shell: "{params.cmd} -p {threads} {params.options} {params.index} -1 {input.read1} -2 {input.read2} | " + samtools_cfg['cmd'] + " view -Sb - > {output}.tmp && mv {output}.tmp {output}"

rule bowtie_build:
    """Bowtie build index"""
    params: ref = bwt_cfg['ref'],
            cmd = 'bowtie-build'
    input: "{prefix}" + ".fa"
    output: expand("{{prefix}}{ext}", ext=bwt_cfg['build']['ext_v1'])
    shell: "{params.cmd} {input} {wildcards.prefix}"

rule bowtie_build2:
    """Bowtie build index"""
    params: ref = bwt_cfg['ref'],
            cmd = 'bowtie2-build'
    input: "{prefix}" + ".fa"
    output: expand("{{prefix}}{ext}", ext=bwt_cfg['build']['ext_v2'])
    shell: "{params.cmd} {input} {wildcards.prefix}"
